apply plugin: 'com.android.application'

android {
  compileSdkVersion 27
  // buildToolsVersion "32.0.0"
  defaultConfig {
    applicationId "com.bristol.snorediaby"
    minSdkVersion 24
    targetSdkVersion 27
    versionCode 1
    versionName "1.0"
    // testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    // 65535上限設置
    multiDexEnabled true
    ndk {
      // 設置支援的SO庫架構
      abiFilters 'armeabi', 'x86', 'armeabi-v7a', 'x86_64', 'arm64-v8a'
    }
  }
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
      resValue "id", "webviewload_monitor_cancel_point", ""
    }
    debug {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      resValue "id", "webviewload_monitor_cancel_point", ""
    }
  }
  compileOptions {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
  }
}

dependencies {
  implementation fileTree(dir: 'libs', include: ['*.jar'])
  implementation 'com.android.support:appcompat-v7:27.1.1'
  implementation 'com.android.support.constraint:constraint-layout:1.1.2'
  implementation 'com.android.support:support-v4:27.1.1'
  implementation 'com.ashokvarma.android:bottom-navigation-bar:2.0.4'
  implementation 'com.anthonycr.grant:permissions:1.0'
  implementation 'com.github.PhilJay:MPAndroidChart:v3.0.1'
  implementation 'com.github.CarGuo:RecordWave:v1.1.8'
  implementation "com.android.support:multidex:1.0.1"
  implementation 'io.reactivex.rxjava2:rxjava:2.1.0'
  implementation 'com.github.vondear:RxTools:v1.7.8.3'
  implementation 'com.tencent.bugly:crashreport_upgrade:1.3.4'
  implementation(
      [group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.4.1'],
      [group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.4.1'],
      [group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.4.1']
  )
  implementation 'com.google.guava:guava:16.0'
  implementation 'org.apache.commons:commons-lang3:3.0'
  // AOP
  implementation 'org.aspectj:aspectjrt:1.8.9'
  // Dagger
  implementation 'com.google.dagger:dagger:2.21'
  annotationProcessor 'com.google.dagger:dagger-compiler:2.21'
  // AutoService
  //  implementation 'com.google.auto.service:auto-service:1.0-rc4'
  implementation 'org.apache.commons:commons-collections4:4.1'

  testImplementation 'junit:junit:4.12'

  androidTestImplementation 'com.android.support.test:runner:1.0.2'
  androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}

import org.aspectj.bridge.IMessage
import org.aspectj.bridge.MessageHandler
import org.aspectj.tools.ajc.Main

final def log = project.logger
final def variants = project.android.applicationVariants

variants.all { variant ->
  if (!variant.buildType.isDebuggable()) {
    log.debug("Skipping non-debuggable build type '${variant.buildType.name}'.")
    return;
  }

  JavaCompile javaCompile = variant.javaCompile
  javaCompile.doLast {
    String[] args = ["-showWeaveInfo",
        "-1.8",
        "-inpath", javaCompile.destinationDir.toString(),
        "-aspectpath", javaCompile.classpath.asPath,
        "-d", javaCompile.destinationDir.toString(),
        "-classpath", javaCompile.classpath.asPath,
        "-bootclasspath", project.android.bootClasspath.join(File.pathSeparator)]
    log.debug "ajc args: " + Arrays.toString(args)

    MessageHandler handler = new MessageHandler(true);
    new Main().run(args, handler);
    for (IMessage message : handler.getMessages(null, true)) {
      switch (message.getKind()) {
        case IMessage.ABORT:
        case IMessage.ERROR:
        case IMessage.FAIL:
          log.error message.message, message.thrown
          break;
        case IMessage.WARNING:
          log.warn message.message, message.thrown
          break;
        case IMessage.INFO:
          log.info message.message, message.thrown
          break;
        case IMessage.DEBUG:
          log.debug message.message, message.thrown
          break;
      }
    }
  }
}
